#!ipxe

set country sk

goto ${menu} ||
menu Debian or Ubuntu ?
item --key d debian Debian [d]
item --key u ubuntu Ubuntu [u]
choose menu || goto deb_exit
goto ${menu}

:debian
set debmirror http://ftp.${country}.debian.org/debian/

set os Debian
menu ${os} ${arch_a}
item --key j jessie ${os} 8 jessie (stable) [j]
item --key s wheezy ${os} 7 wheezy (oldstable) [s]
item --key o squeeze ${os} 6.0 squeeze (oldstable) [o]
item --key t jessie ${os} jessie (testing) [t]
item --key u sid ${os} sid (unstable) [u]
choose ver || goto deb_exit
set ver_alias ${ver}
iseq ${ver} wheezy && set ver_alias stable ||
iseq ${ver} squeeze && set ver_alias oldstable ||
goto deb_boot_type

:ubuntu
#set debmirror http://ftp.ubuntu.com/ubuntu/
set debmirror http://${country}.releases.ubuntu.com/ubuntu/

set os Ubuntu
menu ${os} ${arch_a}
item --gap Current (supported)
item --key v vivid ${os} 15.04 Vivid Vervet [v]
item --key u utopic ${os} 14.10 Utopic Unicorn [u]
item --key t trusty ${os} 14.04 LTS Trusty Tahr [t]
item --key p precise ${os} 12.04 LTS Precise Pangolin [p]
item --gap End of life (unsupported)
item saucy ${os} 13.10 Saucy Salamander
item raring ${os} 13.04 Raring Ringtail
item quantal ${os} 12.10 Quantal Quetzal
item oneiric ${os} 11.10 Oneiric Ocelot
item natty ${os} 11.04 Natty Narwhal
# lucid's pxelinux scripts are not compatible :-(
#item lucid ${os} 10.04.3 LTS Lucid Lynx
item --gap Other versions
item --key D devel Future version (development) [D]
item --key o other Choose other version [o]
choose ver || goto deb_exit

iseq ${ver} other || goto skip_read_ver
echo ${cls}
set ver
echo -n Enter version: ${} && read ver
:skip_read_ver

:deb_boot_type
set dir dists/${ver}/main/installer-${arch_a}/current/images/netboot/

menu ${os} ${ver} boot parameters
item --key k install ${os} install [k]
item --key g gtk ${os} graphical install [g]
item --key c rescue ${os} rescue mode [c]
item --key a automated ${os} automated install [a]
item --key e expert ${os} expert install [e]
item --key l live ${os} live boot [l]
# desktop selection for Debian only
iseq ${menu} debian && item --key d desktop ${os} desktop selection [d] ||
item --key p pxe ${os} pxelinux boot [p]
choose --default ${type} type || goto deb_exit

echo ${cls}
goto deb_${type}

:deb_desktop
menu ${os} desktop selection
item default Default
item kde KDE
item lxde LXDE
item xfce XFCE
choose --default ${desktop} desktop || goto deb_boot_type
iseq desktop default && clear desktop_param || set desktop_param desktop=${desktop}
goto deb_boot_type

:deb_rescue
set install_params rescue/enable=true
goto deb_boot

:deb_automated
set install_params auto=true priority=critical
goto deb_boot

:deb_expert
set install_params priority=low
goto deb_boot

:deb_gtk
set install_params video=vesa:ywrap,mtrr vga=788
set installer gtk/
goto deb_boot

:deb_install
:deb_boot
set dir ${dir}${installer}${menu}-installer/${arch_a}/
imgfree
imgtrust --allow
echo Boot parameters: ${install_params} ${desktop_param} -- quiet ${params}
kernel ${debmirror}${dir}linux ${install_params} ${desktop_param} -- quiet ${params}
initrd ${debmirror}${dir}initrd.gz
boot

:deb_pxe
set 209:string pxelinux.cfg/default
set 210:string ${debmirror}${dir}

imgfree
kernel ${url}pxelinux.0 ${params}
imgverify pxelinux.0 ${sigs}pxelinux.0.sig
boot

:deb_live
menu ${os} desktop selection
item --key g gnome-desktop GNOME [g]
item --key k kde-desktop KDE [k]
item --key l lxde-desktop LXDE [l]
item --key x xfce-desktop XFCE [x]
item --key r rescue Rescue [r]
item --key s standard Standard [s]
choose desktop || goto deb_exit

#nslookup host cdimage.debian.org
#set url http://${host}/debian-cd/current-live/${arch_a}/webboot
nslookup host live.debian.net
set url http://${host}/cdimage/release/${ver_alias}/${arch_a}/web
iseq ${ver_alias} stable && set ver_number 7.6.0 ||
iseq ${ver_alias} oldstable && set ver_number 6.0.10 ||
set deblive ${url}/debian-live-${ver_number}-${arch_a}-${desktop}
imgfree
imgtrust --allow
kernel ${deblive}.vmlinuz boot=live components fetch=${deblive}.squashfs live-config ${params}
initrd ${deblive}.initrd.img
boot

:deb_exit
exit 0
