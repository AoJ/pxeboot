#!ipxe

set country sk

goto ${menu} ||
menu Debian or Ubuntu ?
item --key d debian Debian [d]
item --key u ubuntu Ubuntu [u]
choose menu || goto deb_exit
goto ${menu}

:debian
iseq ${mirror} ${default_mirror} && set debmirror http://ftp.${country}.debian.org/debian/ || set debmirror ${mirror}

set os Debian
menu ${os} ${arch_a}
# https://wiki.debian.org/DebianReleases
item --key 0 buster ${os} 10 buster (stable) [0]
item --key 9 stretch ${os} 9 stretch (oldstable) [9]
item --gap Long Term Support (LTS)
item --key 8 jessie ${os} 8 jessie (stable) [8]
item --gap End of life (unsupported)
item --key 7 wheezy ${os} 7 wheezy [7]
#item --key 6 squeeze ${os} 6.0 squeeze [6]
item --gap Testing and development
item --key t bullseye ${os} bullseye (testing) [t]
item --key u sid ${os} sid (unstable) [u]
choose ver || goto deb_exit
#set ver_alias ${ver}
#iseq ${ver} wheezy && set ver_alias stable ||
#iseq ${ver} squeeze && set ver_alias oldstable ||
goto deb_boot_type

:ubuntu
#set debmirror http://ftp.ubuntu.com/ubuntu/
iseq ${mirror} ${default_mirror} && set debmirror http://${country}.archive.ubuntu.com/ubuntu/ || set debmirror ${mirror}

set os Ubuntu
menu ${os} ${arch_a}
# https://wiki.ubuntu.com/Releases
item --gap Current (supported)
item --key d disco ${os} 19.04 LTS Disco Dingo [d]
item --key c cosmic ${os} 18.10 Cosmic Cuttlefish [c]
item --key b bionic ${os} 18.04 LTS Bionic Beaver [b]
item --key w xenial ${os} 16.04 LTS Xenial Xerus [x]
item --gap Beta versions
item eoan ${os} 19.10 Beta Eoan Ermine
item --gap End of life (unsupported)
item artful ${os} 17.10 Artful Aardvark
item yakkety ${os} 16.10 Yakkety Yak
item wily ${os} 15.10 Wily Werewolf
item vivid ${os} 15.04 Vivid Vervet
item utopic ${os} 14.10 Utopic Unicorn
item trusty ${os} 14.04 LTS Trusty Tahr
item saucy ${os} 13.10 Saucy Salamander
item raring ${os} 13.04 Raring Ringtail
item quantal ${os} 12.10 Quantal Quetzal
item precise ${os} 12.04 Precise Pangolin
item oneiric ${os} 11.10 Oneiric Ocelot
item natty ${os} 11.04 Natty Narwhal
# lucid's pxelinux scripts are not compatible :-(
#item lucid ${os} 10.04.3 LTS Lucid Lynx
item --gap Other versions
item --key D devel Future version (development) [D]
item --key o other Choose other version [o]
choose ver || goto deb_exit

iseq ${ver} other || goto skip_read_ver
echo ${cls}
set ver
echo -n Enter version: ${} && read ver
:skip_read_ver

:deb_boot_type
set diru dists/${ver}-updates/main/installer-${arch_a}/current/images/netboot/
set dir dists/${ver}/main/installer-${arch_a}/current/images/netboot/

menu ${os} ${ver} boot parameters
item --key i install ${os} install [i]
item --key g gtk ${os} graphical install [g]
item --key a automated ${os} automated install [a]
iseq os Debian && item --key k kickstart ${os} kickstart install [k] ||
item --key c rescue ${os} rescue mode [c]
item --key e expert ${os} expert install [e]
item --key l live ${os} live boot [l]
# desktop selection for Debian only
iseq ${menu} debian && item --key d desktop ${os} desktop selection [d] ||
item --key p pxe ${os} pxelinux boot [p]
choose --default ${type} type || goto deb_exit

echo ${cls}
goto deb_${type}

:deb_desktop
menu ${os} desktop selection
item default Default
item kde KDE
item lxde LXDE
item xfce XFCE
choose --default ${desktop} desktop || goto deb_boot_type
iseq ${desktop} default && clear desktop_param || set desktop_param desktop=${desktop}
goto deb_boot_type

:deb_rescue
set install_params rescue/enable=true
goto deb_boot

:deb_automated
set install_params auto=true priority=critical url=https://www.salstar.sk/boot/ks/deb/preseed
goto deb_boot

:deb_expert
set install_params priority=low
goto deb_boot

:deb_gtk
set install_params video=vesa:ywrap,mtrr vga=788
iseq ${ver} precise && set installer gtk/ ||
goto deb_boot

:deb_kickstart
set install_params ks=http://boot.salstar.sk/ks/ubuntu
goto deb_boot

:deb_install
:deb_boot
set diru ${diru}${installer}${menu}-installer/${arch_a}/
set dir ${dir}${installer}${menu}-installer/${arch_a}/
imgfree
imgtrust --allow
echo Boot parameters: ${install_params} ${desktop_param} -- quiet ${params}
# fallback to non-updates dir
kernel ${debmirror}${dir}linux ${install_params} ${desktop_param} -- quiet ${params} || kernel ${debmirror}${diru}linux ${install_params} ${desktop_param} -- quiet ${params}
initrd ${debmirror}${dir}initrd.gz || initrd ${debmirror}${diru}initrd.gz
boot

:deb_pxe
set 209:string pxelinux.cfg/default
set 210:string ${debmirror}${dir}

imgfree
kernel ${url}pxelinux.0 ${params}
imgverify pxelinux.0 ${sigs}pxelinux.0.sig
boot

:deb_live
menu ${os} desktop selection
item --key g gnome-desktop GNOME [g]
item --key k kde-desktop KDE [k]
item --key l lxde-desktop LXDE [l]
item --key x xfce-desktop XFCE [x]
item --key r rescue Rescue [r]
item --key s standard Standard [s]
choose desktop || goto deb_exit

nslookup host cdimage.debian.org
set url http://${host}/debian-cd/current-live/${arch_a}/webboot
#iseq ${ver_alias} stable && set ver_number 7.6.0 ||
#iseq ${ver_alias} oldstable && set ver_number 6.0.10 ||
# only latest version available :-(
set ver_number 8.8.0
echo Check debian-live version on this URL:
echo ${}  ${url}
echo -n Enter version: ${}
read ver_number
#nslookup host live.debian.net
#set url http://${host}/cdimage/release/${ver_alias}/${arch_a}/web
set deblive ${url}/debian-live-${ver_number}-${arch_a}-${desktop}
imgfree
imgtrust --allow
kernel ${deblive}.vmlinuz boot=live components fetch=${deblive}.squashfs live-config ${params}
initrd ${deblive}.initrd.img
boot

:deb_exit
exit 0
