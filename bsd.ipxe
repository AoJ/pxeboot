#!ipxe

menu BSD systems
item --key o openbsd OpenBSD [o]
item --key n netbsd NetBSD [n]
item --key f freebsd FreeBSD [f]
item --key p pcbsd PC-BSD [p]
choose bsd || goto bsd_exit

goto ${bsd}
:bsd_exit
exit 0

:boot_bsd
echo ${cls}
imgfree
imgtrust --allow
echo URL: ${mirror}${dir}/${iso}
initrd ${mirror}${dir}/${iso} || initrd ${mirror}${dir}/${iso2}
chain ${url}memdisk iso raw
exit

:openbsd
set os OpenBSD
menu Choose major version for ${os} ${arch_a}
item 5 ${os} 5
item other ${os} Choose other version
choose ver_major || exit 0

iseq ${ver_major} other || goto skip_read_ver_major_openbsd
echo ${cls}
echo -n Enter major version: ${} && read ver_major
:skip_read_ver_major_openbsd

menu Choose minor version for ${os} ${ver_major} ${arch_a}
item 6 ${os} ${ver_major}.6
item 5 ${os} ${ver_major}.5
item 4 ${os} ${ver_major}.4
item 3 ${os} ${ver_major}.3
item 2 ${os} ${ver_major}.2
item other ${os} Choose other version
choose ver_minor || exit 0

iseq ${ver_minor} other || goto skip_read_ver_minor_openbsd
echo ${cls}
echo -n Enter minor version: ${} && read ver_minor
:skip_read_ver_minor_openbsd

set ver ${ver_major}.${ver_minor}
set verpl ${ver_major}${ver_minor}
#set mirror http://ftp.openbsd.org/pub/
set mirror http://mirror.steadynet.cz/pub/
set dir OpenBSD/${ver}/${arch_a}
set iso cd${verpl}.iso
goto boot_bsd

:netbsd
set os NetBSD
menu ${os} ${arch_a}
item 6.1.3 ${os} 6.1.3
item 6.0.4 ${os} 6.0.4
item 5.2.2 ${os} 5.2.2
item 5.1.4 ${os} 5.1.4
item other ${os} Choose other version
choose ver || exit 0

iseq ${ver} other || goto skip_read_ver_netbsd
echo ${cls}
echo -n Enter version: ${} && read ver
:skip_read_ver_netbsd

set mirror http://iso.se.netbsd.org/pub/
set dir NetBSD/iso/${ver}
set iso2 ${arch_a}cd-${ver}.iso
set iso NetBSD-${ver}-${arch_a}.iso
goto boot_bsd

:freebsd
set os FreeBSD
menu ${os}
item 10.1 ${os} 10.1
item 9.2 ${os} 9.2
item 8.4 ${os} 8.4
item other ${os} Choose other version
choose ver || exit 0

iseq ${ver} other || goto skip_read_ver_freebsd
echo ${cls}
echo -n Enter version: ${} && read ver
:skip_read_ver_freebsd

set mirror http://ftp.cz.freebsd.org/pub/
set dir FreeBSD/releases/ISO-IMAGES/${ver}
set iso FreeBSD-${ver}-RELEASE-${arch_a}-bootonly.iso
goto boot_bsd

:pcbsd
set os PC-BSD
menu ${os}
item 10.0-RELEASE ${os} 10.0
item 9.2-RELEASE ${os} 9.2
item 9.1-RELEASE ${os} 9.1
item 9.0 ${os} 9.0
item other ${os} Choose other version
choose ver || exit 0

iseq ${ver} other || goto skip_read_ver_pcbsd
echo ${cls}
echo -n Enter version: ${} && read ver
:skip_read_ver_pcbsd

set mirror http://ftp.heanet.ie/mirrors/pcbsd/iso/
iseq ${arch} i386 && set xarch x86 || set xarch x64
set dir ${ver}/${arch_a}
#set iso PCBSD${ver}-${xarch}-bootonly.iso
set iso latest.iso
goto boot_bsd
