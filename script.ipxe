#!ipxe

:start
set url https://pxe.salstar.sk/
set mirror http://ftp.upjs.sk${mirror_port}/pub
set sigs ${url}sigs/

set cls:hex 1b:5b:4a  # ANSI clear screen sequence - "^[[J"

set memtest_version 4.20

isset ${admin_ip} || set admin_ip 158.197.240.41
isset ${arch} || set arch x86_64
isset ${menu} && goto ${menu} ||

isset ${ip} || dhcp || echo DHCP failed

imgtrust

:main_menu
clear version
set space:hex 20:20
menu SAL's iPXE MENU
item --gap Default:
item --key l local ${space} Boot local hdd [l]
item --gap Operating systems:
iseq ${menu} fedora && set default --default || set default --
item --key f ${default} fedora ${space} Fedora ${arch} [f]
iseq ${menu} centos && set default --default || set default --
item --key c ${default} centos ${space} CentOS ${arch} [c]
iseq ${menu} debian && set default --default || set default --
item --key b ${default} debian ${space} Debian [b]
iseq ${menu} ubuntu && set default --default || set default --
item --key u ${default} ubuntu ${space} Ubuntu [u]
iseq ${menu} opensuse && set default --default || set default --
item --key o ${default} opensuse ${space} openSUSE [o]
iseq ${menu} pmagic && set default --default || set default --
item --key p ${default} pmagic ${space} Parted Magic [p]
iseq ${menu} dos && set default --default || set default --
item --key d ${default} dos ${space} FreeDOS [d]
item --gap Options:
iseq ${arch} x86_64 && set bits 64 || set bits 32
iseq ${menu} changebits && set default --default || set default --
item --key a ${default} changebits ${space} Architecture: ${arch} (${bits}bit) [a]
iseq ${menu} params_menu && set default --default || set default --
item --key 0x09 ${default} params_menu ${space} Kernel parameters: ${params}
item --gap Tools:
item --key x pxelinux ${space} Chainload pxelinux menu [x]
item --key h hdt ${space} Hardware detection tool [h]
item --key m memtest ${space} MemTest86+ ${memtest_version} [m]
item --key s shell ${space} iPXE shell [s]
isset ${menu} && set timeout 0 || set timeout 60000
choose --timeout ${timeout} menu || goto local
echo ${cls}
goto ${menu}

:local
echo Booting from local disks ...
exit 0

:reload
echo Reloading script.ipxe ...
imgverify script.ipxe ${sigs}script.ipxe
chain script.ipxe

:pxelinux
echo Loading pxelinux ...
set next-server 158.197.16.70
set 209:string pxelinux.cfg/default
set 210:string tftp://${next-server}/
kernel ${url}pxelinux.0
imgverify pxelinux.0 ${sigs}pxelinux.0.sig
boot

:hdt
set 209:string hdt
set 210:string ${url}pxelinux.cfg/
kernel ${url}pxelinux.0
imgverify pxelinux.0 ${sigs}pxelinux.0.sig
boot
goto main_menu

:pmagic
imgverify pmagic.ipxe ${sigs}pmagic.ipxe.sig
chain pmagic.ipxe
goto main_menu

:dos
imgverify dos.ipxe ${sigs}dos.ipxe.sig
chain dos.ipxe
goto main_menu

:memtest
kernel ${url}images/memtest
imgverify memtest ${sigs}images/memtest.sig
boot 

:shell
echo Type "exit" to return to menu.
# temporarily disable imgtrust for shell
imgtrust --allow
set menu main_menu
shell
imgtrust
goto main_menu

:changebits
iseq ${arch} x86_64 && set arch i386 || set arch x86_64
goto main_menu

:params_menu
menu Kernel parameters
item --gap Current: ${params}
item --gap
#item --key x exit Return to main menu [x]
item --key s set Set parameters [s]
item --key v vnc VNC installation [v]
item --key 0 ttyS0 Serial console ttyS0 [0]
item --key 1 ttyS1 Serial cosnole ttyS1 [1]
choose kp || goto main_menu
echo ${cls}
goto params_${kp}

# Kernel parameters
:params_set
echo -n Enter parameters: ${} && read params
goto params_menu

:params_vnc
set params ${params} vnc vncconnect=${admin_ip}:5500
goto params_menu

:params_ttyS0
set params ${params} console=ttyS0
goto params_menu

:params_ttyS1
set params ${params} console=ttyS1
goto params_menu

:params_exit
goto main_menu

# OS
:fedora
:centos
imgverify fedora.ipxe ${sigs}fedora.ipxe.sig
chain fedora.ipxe
goto main_menu

:debian
:ubuntu
imgverify deb.ipxe ${sigs}deb.ipxe.sig
chain deb.ipxe
goto main_menu

:opensuse
imgverify opensuse.ipxe ${sigs}opensuse.ipxe.sig
chain opensuse.ipxe
goto main_menu
